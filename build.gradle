plugins {
  id 'java' // core plugin dependency
  id 'idea' // adds nice commands like 'openIdea'
  id 'com.github.johnrengelman.shadow' version "$shadowJarVersion" // requirement for shadowJar task
}

repositories {
    mavenCentral() // where plugins in plugins{} block are retrieved, as well as dependencies.
}

group = 'com.acantilado'
version = '1.0.0-SNAPSHOT'
description = "Acantilado"
allprojects {
    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_19
        targetCompatibility = JavaVersion.VERSION_19
    }
}

allprojects {
    plugins.withType(JavaPlugin).tap {
        configureEach {
            dependencies {
                implementation group: 'io.dropwizard', name: 'dropwizard-core', version: "$dropwizardVersion"
                implementation group: 'io.dropwizard', name: 'dropwizard-auth', version: "$dropwizardVersion"
                implementation group: 'io.dropwizard', name: 'dropwizard-assets', version: "$dropwizardVersion"
                implementation group: 'io.dropwizard', name: 'dropwizard-http2', version: "$dropwizardVersion"
                implementation group: 'io.dropwizard', name: 'dropwizard-hibernate', version: "$dropwizardVersion"
                implementation group: 'io.dropwizard', name: 'dropwizard-migrations', version: "$dropwizardVersion"
                implementation group: 'io.dropwizard', name: 'dropwizard-views-freemarker', version: "$dropwizardVersion"
                implementation group: 'io.dropwizard', name: 'dropwizard-views-mustache', version: "$dropwizardVersion"
                implementation group: 'io.dropwizard', name: 'dropwizard-metrics-graphite', version: "$dropwizardVersion"
                implementation group: 'io.federecio', name: 'dropwizard-swagger', version: "0.7.0"
                implementation group: 'com.smoketurner', name: 'dropwizard-swagger', version: "$dropwizardVersion"
                implementation group: 'com.h2database', name: 'h2', version: "$h2Version"
                implementation group: 'com.google.guava', name: 'guava', version: "$guavaVersion"
                implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jacksonVersion"
                testImplementation group: 'io.dropwizard', name: 'dropwizard-testing', version: "$dropwizardVersion"
                testImplementation(group: 'org.glassfish.jersey.test-framework.providers',
                        name: 'jersey-test-framework-provider-inmemory', version: "$jerseyTestFrameworkProviderVersion") {
                    exclude(module: 'javax.servlet-api')
                    exclude(module: 'junit')
                }
                testImplementation(group: 'org.glassfish.jersey.test-framework.providers',
                        name: 'jersey-test-framework-provider-grizzly2', version: "$jerseyTestFrameworkProviderVersion") {
                    exclude(module: 'javax.servlet-api')
                    exclude(module: 'junit')
                }
                testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: "$jupiterVersion"
                testImplementation group: 'org.mockito', name: 'mockito-core', version: "$mockitoVersion"
                testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: "$mockitoVersion"

            }
        }
    }
}

dependencies {
    implementation project(path: ':acantilado-app')
    implementation project(path: ':acantilado-model')
    implementation project(path: ':acantilado-seeder')
    implementation project(path: ':acantilado-repository')
}

shadowJar {
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'com.acantilado.AcantiladoApplication'
    }
}

test {
    useJUnitPlatform()
}